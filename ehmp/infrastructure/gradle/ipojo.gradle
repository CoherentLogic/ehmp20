buildscript { 
    repositories { 
        mavenCentral()
    }   
    dependencies { 
        classpath 'org.apache.felix:org.apache.felix.ipojo.manipulator:1.8.6'
    }   
}

dependencies {
    compile 'org.apache.felix:org.apache.felix.ipojo.annotations:1.8.6'
}


/**
 * Gradle task for iPOJO Bundle <a href="http://felix.apache.org/site/dive-into-the-ipojo-manipulation-depths.html">manipulation</a>.
 * At build-time, this task creates a new manipulated jar, deletes the original and renames the manipulated jar file.
 */
jar.doLast { 
	org.apache.felix.ipojo.manipulator.Pojoization pojo = new org.apache.felix.ipojo.manipulator.Pojoization()
	
	File jarfile = file(jar.archivePath)
	File targetJarFile = file(jar.destinationDir.absolutePath +"/" + jar.baseName + "_out.jar")
	
	if (!jarfile.exists()) throw new InvalidUserDataException("The specified bundle file does not exist: " + jarfile.absolutePath)
	
	pojo.pojoization(jarfile, targetJarFile, (File) null)
	
	pojo.getWarnings().each { s -> 
		println s
    }
	
	if (jarfile.delete()) {
		if ( !targetJarFile.renameTo(jarfile) ) {
			throw new InvalidUserDataException("Cannot rename the manipulated jar file");
		}
	} else {
		throw new InvalidUserDataException("Cannot delete the input jar file")
	}
}

